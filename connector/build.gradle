plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.0.0-beta6'
    id 'com.diffplug.spotless' version '7.0.2'
}

repositories {
    mavenCentral()
}

def mockitoVersion = "5.15.2"
def kafkaVersion = "3.8.0"
def lhVersion = "0.12.5"
def slf4jVersion = "2.0.16"
def lombokVersion = "1.18.36"

dependencies {
    // littlehorse
    implementation "io.littlehorse:littlehorse-client:${lhVersion}"

    // kafka connect
    compileOnly "org.apache.kafka:connect-api:${kafkaVersion}"
    compileOnly "org.apache.kafka:connect-runtime:${kafkaVersion}"

    // log
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-reload4j:${slf4jVersion}"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // tests
    testImplementation "org.apache.kafka:connect-api:${kafkaVersion}"
    testImplementation "org.apache.kafka:connect-runtime:${kafkaVersion}"
    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation 'net.datafaker:datafaker:2.4.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

shadowJar {
    mergeServiceFiles()
    destinationDirectory = layout.buildDirectory.dir("bundle/lh-kafka-connect")
    archiveBaseName = 'lh-kafka-connect'
    archiveClassifier = ''
    relocate 'io.grpc', 'shadow.io.grpc'
    manifest {
        attributes 'Implementation-Version': "${version}"
    }
}

spotless {
    java {
        removeUnusedImports()
        prettier(['prettier': '3.0.3', 'prettier-plugin-java': '2.3.0'])
                .config(['parser': 'java', 'tabWidth': 4, 'printWidth': 80, 'plugins': ['prettier-plugin-java']])
    }
}
